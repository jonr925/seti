<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:components="components.*"
		currentState="loading"
		title="Stars" 
		actionBarVisible="false"
		creationComplete="view1_creationCompleteHandler(event)" xmlns:skinnablecomponents="com.hg94.seti.skinnablecomponents.*">
	<s:states>
		<s:State name="loading"/>
		<s:State name="starsOnly"/>
		<s:State name="showDetails"/>
	</s:states>
	<fx:Script>
		<![CDATA[
			import com.google.maps.LatLng;
			import com.google.maps.Map;
			import com.google.maps.MapEvent;
			import com.google.maps.MapMouseEvent;
			import com.google.maps.MapOptions;
			import com.google.maps.extras.planetary.Sky;
			import com.google.maps.overlays.Marker;
			import com.google.maps.overlays.MarkerOptions;
			import com.google.maps.styles.FillStyle;
			import com.hg94.seti.controller.remote.TargetListRequest;
			import com.hg94.seti.model.SETIQuestDataReader;
			import com.hg94.seti.model.Target;
			import com.hg94.seti.model.TargetSet;
			
			import components.StarfieldTargetMarkerSkin;
			
			import mx.events.FlexEvent;
			
			private static const STATE_STARS_ONLY:String = "starsOnly";
			private static const STATE_SHOW_DETAILS:String = "showDetails";
			private static const HATHERSAGE_MAP_API_KEY:String = "ABQIAAAAEwk3rg5igZgbfHAAaPpdnhS3hwEVAxBLwgX-yCFBjg8qlFo5UxTVUCdE50GpyF9WvX4b62ZJcx0ASA";
			
			protected var _targetSet:TargetSet;
			protected var _currentTarget:Target;
			protected var _targetArray:Array = [];
			
			protected function loadTargetList():void 
			{
				var targetRequest:TargetListRequest = new TargetListRequest();
				targetRequest.addEventListener(Event.COMPLETE, onTargetLoadComplete);
				targetRequest.execute();
			}
			
			private function onTargetLoadComplete(event:Event):void 
			{
				event.currentTarget.removeEventListener(event.type, onTargetLoadComplete);
				_targetSet = (event.currentTarget as TargetListRequest).targetSet;
				
				for each (var target:Target in this._targetSet.targetArray) {
					_targetArray.push(target.getGoogleSkyCoordinates());
					var marker:Marker = new Marker(target.getGoogleSkyCoordinates());
					marker.addEventListener(MapMouseEvent.CLICK, onMarkerInteraction);
					var markerOptions:MarkerOptions = marker.getOptions();
					markerOptions.icon = new StarfieldTargetMarkerSkin();
					markerOptions.iconOffset = new Point(-8,-8);
					marker.setOptions(markerOptions);
					map.addOverlay(marker);
				}
				
				currentState=STATE_STARS_ONLY;
			}
			
			private function onMapPreinitialize(event:Event):void 
			{
				var opts:MapOptions = new MapOptions();
				opts.mapTypes = [Sky.VISIBLE_MAP_TYPE];
				opts.zoom = 2;
				opts.backgroundFillStyle = new FillStyle();
				opts.backgroundFillStyle.color = 0x000000;
				event.target.setInitOptions(opts);
			}
			
			private function onMapReady(event:MapEvent):void
			{
				event.target.addMapType(Sky.VISIBLE_MAP_TYPE);
			
				loadTargetList();
			}
			
			private function onMarkerInteraction(event:MapMouseEvent):void
			{
				var currentLatLng:LatLng = event.latLng;
				var currentPoint:Point = map.fromLatLngToPoint(event.latLng);
				_currentTarget = this._targetSet.getTargetByGoogleSkyCoordinates(currentLatLng);
				currentState = STATE_SHOW_DETAILS;
			}
			
			protected function onExploreObservation(event:Event):void
			{
				currentState = STATE_SHOW_DETAILS;
				navigator.pushView(com.hg94.seti.views.WaterfallExplorerView, _currentTarget);
			}
			
			[Bindable (event="click")]
			private function get rightAscension():String 
			{
				return _currentTarget.rightAscension.toString();
			}
			
			[Bindable (event="click")]
			private function get declination():String 
			{
				return _currentTarget.declination.toString();
			}
			
			[Bindable (event="click")]
			private function get friendlyName():String
			{
				return _currentTarget.friendlyName;
			}
			

			protected function zoomInButton_clickHandler(event:MouseEvent):void
			{
				this.map.zoomIn();
			}


			protected function zoomOutButton_clickHandler(event:MouseEvent):void
			{
				this.map.zoomOut();
			}
			
			private var map:Map;
			
			protected function view1_creationCompleteHandler(event:FlexEvent):void
			{
				this.starfieldSkin.zoomInButton.addEventListener(MouseEvent.CLICK, this.zoomInButton_clickHandler);
				this.starfieldSkin.zoomOutButton.addEventListener(MouseEvent.CLICK, this.zoomOutButton_clickHandler);
				
				map = new Map();
				map.key=HATHERSAGE_MAP_API_KEY;
				map.url="http://hathersagegroup.com"
				map.sensor="false"
				map.percentWidth=100;
				map.percentHeight=100;
				map.addEventListener(MapEvent.MAP_PREINITIALIZE,onMapPreinitialize);
				map.addEventListener(MapEvent.MAP_READY,onMapReady);
				
				mapGroup.addElement(map);
			}

		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<s:Group width="100%" height="100%">
		<components:SplashSkin visible="false" visible.loading="true"/>
		<s:Group id="mapGroup" width="100%" height="100%"/>
		<components:StarfieldSkin id="starfieldSkin"/>
		<s:Group right="0" top="50"
				 width.showDetails="30%" width="0" 
				 height="100%">
			<s:layout>
				<s:VerticalLayout gap="10"/>
			</s:layout>
			<s:Button label="Explore observation:" click="onExploreObservation(event)"/>
			<s:Label text="{'Ra: ' + rightAscension }"/>
			<s:Label text="{'Decl: ' + declination }"/>
			<s:Label text="{friendlyName}"/>
		</s:Group>
	</s:Group>
</s:View>
